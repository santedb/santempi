; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SanteMPI"
#define MyAppPublisher "SanteDB Community"
#define MyAppURL "http://santesuite.org"
#define MyAppVersion "2.1.60"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E2A094E4-0E7E-4C21-9283-4F169DB35CF4}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf64}\SanteSuite\SanteDB\Server
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=..\License.rtf
OutputDir=..\bin\release\dist\
OutputBaseFilename = santempi-{#MyAppVersion}
SolidCompression=yes
Uninstallable=true
WizardSmallImageFile=.\install-small.bmp
WizardImageFile=.\install.bmp
#ifdef DEBUG
Compression = none
#else
Compression = lzma
#endif
AppCopyright = Copyright (C) 2015-2021 SanteSuite Contributors
ArchitecturesInstallIn64BitMode = x64
ArchitecturesAllowed =  x64
WizardStyle=modern

#ifndef UNSIGNED
SignedUninstaller=yes
SignTool=default sign /a /n $qFyfe Software$q /d $qSanteDB iCDR Server$q $f
#endif
; SignTool=default sign $f
; SignedUninstaller=yes

[Files]

Source: ..\bin\release\config\*.*; DestDir: {app}\config; 
Source: ..\bin\release\config\template\*.*; DestDir: {app}\config;

; Microsoft .NET Framework 4.5 Installation
Source: .\netfx.exe; DestDir: {tmp} ; Flags: dontcopy

; VC Redist for FBSQL
Source: .\vc2010.exe; DestDir: {tmp} ; Flags: dontcopy

; SanteDB Server Installer
Source: .\santedb-server.exe; DestDir: {tmp}; Flags: dontcopy deleteafterinstall;

; SanteMPI Integration
Source: ..\bin\Release\SanteMPI.*; DestDir: {app}; 

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Run]
Filename: "{app}\ConfigTool.exe";  Description: "Configure SanteDB Server"; Flags: postinstall ; 
Filename: "{app}\SanteDB.exe"; Parameters:"--install"; Flags: runhidden runascurrentuser; StatusMsg: "Registering SanteDB Service"
Filename: "c:\windows\system32\netsh.exe"; Parameters: "advfirewall firewall add rule name=""SanteDB REST Ports"" dir=in protocol=TCP localport=8080 action=allow"; StatusMsg: "Configuring Firewall"; Flags: runhidden; 
Filename: "c:\windows\system32\netsh.exe"; Parameters: "advfirewall firewall add rule name=""SanteDB HL7 Ports"" dir=in protocol=TCP localport=2100 action=allow"; StatusMsg: "Configuring Firewall"; Flags: runhidden; 


[UninstallRun]
Filename: "{app}\SanteDB.exe"; Parameters: "--uninstall"; StatusMsg: "Un-registering SanteDB"; Flags:runhidden runascurrentuser;

[Icons]
Name: "{commonprograms}\SanteDB\SanteDB Server Console"; Filename: "{app}\sdbac.exe"
Name: "{commonprograms}\SanteDB\SanteDB Server Configuration"; Filename: "{app}\configtool.exe"
Filename: "http://help.santesuite.org"; Name: "{group}\SanteDB\SanteDB Help"; IconFilename: "{app}\santedb.exe"

; Components
[Code]


const
  SHCONTCH_NOPROGRESSBOX = 4;
  SHCONTCH_RESPONDYESTOALL = 16;

procedure UnZip(ZipPath, TargetPath: string); 
var
  Shell: Variant;
  ZipFile: Variant;
  TargetFolder: Variant;
begin
  Shell := CreateOleObject('Shell.Application');

  ZipFile := Shell.NameSpace(ZipPath);
  if VarIsClear(ZipFile) then
    RaiseException(
      Format('ZIP file "%s" does not exist or cannot be opened', [ZipPath]));

  TargetFolder := Shell.NameSpace(TargetPath);
  if VarIsClear(TargetFolder) then
    RaiseException(Format('Target path "%s" does not exist', [TargetPath]));

  TargetFolder.CopyHere(
    ZipFile.Items, SHCONTCH_RESPONDYESTOALL);
end;

function PrepareToInstall(var needsRestart:Boolean): String;
var
  hWnd: Integer;
  ResultCode : integer;
  uninstallString : string;
begin
    EnableFsRedirection(true);
    WizardForm.PreparingLabel.Visible := True;
    WizardForm.PreparingLabel.Caption := 'Installing Visual C++ Redistributable';
    ExtractTemporaryFile('vc2010.exe');
    Exec(ExpandConstant('{tmp}\vc2010.exe'), '/install /passive', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
    WizardForm.PreparingLabel.Caption := 'Installing Microsoft .NET Framework 4.8';
    ExtractTemporaryFile('netfx.exe');
    Exec(ExpandConstant('{tmp}\netfx.exe'), '/q', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);

    if(not(FileExists(ExpandConstant('{app}\SanteDB.exe')))) then begin
      
      WizardForm.PreparingLabel.Caption := 'Installing SanteDB iCDR Server';
      ExtractTemporaryFile('santedb-server.exe');
      Exec(ExpandConstant('{tmp}\netfx.exe'), '/SILENT /FORCECLOSEAPPLICATIONS  /DIR=' + ExpandConstant('{app}'), '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
     
    end; // if
end;

// Removes the 2.0 Assets which may be present in the installation directory
procedure Remove20Assets() ;
var 
  files : Array [0..7] of string;
  i : integer;
begin
  files[0] := ExpandConstant('{app}\SanteDB.Core.dll');
  files[1] := ExpandConstant('{app}\SanteGuard.Core.dll');
  files[2] := ExpandConstant('{app}\SanteGuard.Messaging.Ami.dll');
  files[3] := ExpandConstant('{app}\SanteGuard.Messaging.Syslog.dll');
  files[4] := ExpandConstant('{app}\SanteGuard.Persistence.Ado.dll');
  files[5] := ExpandConstant('{app}\SanteMPI.Messaging.PixPdqv2.dll');
  files[6] := ExpandConstant('{app}\SanteMPI.Persistence.ADO.dll');
  
  if(FileExists(files[1]) and (MsgBox('You appear to have SanteDB 2.0.x plugins which might not be compatible with this version. Would you like to remove them?', mbConfirmation, MB_YESNO) = idYes)) then begin
    for i :=  0 to 7 do begin
      if(FileExists(files[i])) then begin
        try 
          DeleteFile(files[i]);
        except 
          ShowExceptionMessage();
        end;
      end; // if
    end; // for
  end;
end;


procedure CurPageChanged(CurPageID: Integer);
begin
  if((CurPageID = wpInstalling)) then begin
    Remove20Assets();
  end;
end;
